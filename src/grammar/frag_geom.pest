# hidden tokens
rsep = _{ "-" }
bopen = _{ "[" }
bclose = _{ "]" }
usep = _{ ":" }
dopen = _{ "{" }
dclose = _{ "} }

len = { len_range | single_len }
  len_range = @{ ASCII_DIGIT+ ~ rsep ~ ASCII_DIGIT+ }
  single_len = { ASCII_DIGIT+ }

bounded_segment = { 
  bounded_umi_segment |
  bounded_read_segment |
  bounded_barcode_segment |
  bounded_fixedseq_segment |
  bounded_discard_segment 
}
  bounded_umi_segment = { "u" ~ bopen ~ len ~ bclose }
  bounded_read_segment = { "r" ~ bopen ~ len ~ bclose }
  bounded_barcode_segment = { "b" ~ bopen ~ len ~ bclose }
  bounded_fixedseq_segment = { "f" ~ bopen ~ nucstr ~ bclose }
    nucstr = { ("A" | "C" | "G" | "T")+ }
  bounded_discard_segment = { "x" ~ bopen ~ len ~ bclose }

unbounded_segment = {
  unbounded_umi_segment |
  unbounded_read_segment |
  unbounded_barcode_segment |
  unbounded_discard_segment
}
  unbounded_umi_segment = { "u" ~ usep }
  unbounded_read_segment = { "r" ~ usep }
  unbounded_barcode_segment = { "b" ~ usep }
  unbounded_discard_segment = { "x" ~ usep }

read_desc = { dopen ~ ( unbounded_segment | ( bounded_segment+ ~ unbounded_segment? ) ) ~ dclose }
frag_desc = _{ read_1_desc ~ read_2_desc }
  read_1_desc = { "1" ~ read_desc }
  read_2_desc = { "2" ~ read_desc }

